(in-ns 'user)

;; DEBUGGING
(def log-store (atom {}))

(defn debug [tag val]
  (swap! log-store update-in [tag] #(conj (or % []) val))
  val)

(defn logs
  [tag & functions]
  (let [tag (if (number? tag)
              (nth (keys @log-store) tag)
              tag)]
    (loop [values    (@log-store tag)
           functions functions]
      (if (seq functions)
        (recur ((first functions) values)
               (rest functions))
        values))))

(defn same-values? [x]
  (if (= 1 (count (set x)))
    :same-values
    :different-values))

(defn tags []
  (->> @log-store
       (reduce-kv #(assoc %1 [(count %3) (same-values? %3) %2] (last %3)) {})
       (map-indexed hash-map)
       (into [])))

(defn debug-data-reader [form]
  `(debug (quote ~form) ~form))
;;

;; PROFILING
(defn heap []
  (let [u (.getHeapMemoryUsage (java.lang.management.ManagementFactory/getMemoryMXBean))
        used (/ (.getUsed u) 1e6)
        total (/ (.getMax u) 1e6)]
    (format "Used: %.0f/%.0f MB (%.0f%%)" used total (/ used total 0.01))))

(let [time*
      (fn [^long duration-in-ms f]
        (let [^com.sun.management.ThreadMXBean bean (java.lang.management.ManagementFactory/getThreadMXBean)
              bytes-before (.getCurrentThreadAllocatedBytes bean)
              duration (* duration-in-ms 1000000)
              start (System/nanoTime)
              first-res (f)
              delta (- (System/nanoTime) start)
              deadline (+ start duration)
              tight-iters (max (quot (quot duration delta) 10) 1)]
          (loop [i 1]
            (let [now (System/nanoTime)]
              (if (< now deadline)
                (do (dotimes [_ tight-iters] (f))
                    (recur (+ i tight-iters)))
                (let [i' (double i)
                      bytes-after (.getCurrentThreadAllocatedBytes bean)
                      t (/ (- now start) i')]
                  (println
                    (format "Time per call: %s   Alloc per call: %,.0fb   Iterations: %d"
                            (cond (< t 1e3) (format "%.0f ns" t)
                                  (< t 1e6) (format "%.2f us" (/ t 1e3))
                                  (< t 1e9) (format "%.2f ms" (/ t 1e6))
                                  :else (format "%.2f s" (/ t 1e9)))
                            (/ (- bytes-after bytes-before) i')
                            i))
                  first-res))))))]

  (defmacro time+
    "Like `time`, but runs the supplied body for 2000 ms and prints the average
  time for a single iteration. Custom total time in milliseconds can be provided
  as the first argument. Returns the returned value of the FIRST iteration."
    [?duration-in-ms & body]
    (let [[duration body] (if (integer? ?duration-in-ms)
                            [?duration-in-ms body]
                            [2000 (cons ?duration-in-ms body)])]
      `(~time* ~duration (fn [] ~@body)))))

(defmacro perf-tools []
  '(do
     (require '[clj-async-profiler.core :as prof])
     (require '[clj-java-decompiler.core :refer [decompile]])
     (require '[clj-memory-meter.core :as mm])
     (require '[criterium.core :as crit])

     (.refer *ns* 'time+ #'user/time+)
     (.refer *ns* 'heap #'user/heap)))
;;

(println "Loaded system-wide user.clj!")
